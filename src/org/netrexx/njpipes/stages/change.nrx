-- change.nrx NJPipe Stage
/*
 *  Copyright (C) 1998  Jeffrey Hennick, JHennick@Delphi.Com
 *Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/*
  20/10/08 Added FROM, TO, FROMTO, and FOR optional keywords.  Jeff Hennick
  98/09/20 removed the StageExit, it was not helping much...  ejt
  98/07/30 SelIRange WAS correct...  ejt
  98/07/30 use StageError(13,... in stageExit.  ejt
  98/07/18 Sel1IRange should have been called instead of SelIRange.  ejt
  98/06/29 Fix for range of 2.1 .  JHennick
  98/06/12 DString.  IRange.  Up to CMS documentation.  JHennick
*/

/** change

  >>--CHANGE--+-------------+--+-----------------+---------------------------->
              +-| anycase |-+  +-inputRange------+
                               |    <-------+    |
                               +-(----range-+--)-+

        +-FROMTO-(1)+
  >--+--+-----------+-| changeString |--------------------+-->
     | +-FROM-(1)+                 +-TO-(1)+                 |
     +-+---------+-delimitedString-+-------+-delimitedString-+

  >---+----------------------+---><
      | +-FOR-(1)+           |
      +-+--------+-numorstar-+


  changeString:
  |--delimiter--string--delimiter--string--delimiter--|

  anycase:
     +-RESPECTCASE-(1)+
  |--+----------------+--|
     +-ANYcase--------+
     +-CASEANY--------+
     +-CASEIGNORE-----+
     +-IGNORECASE-----+
     +-CASSLESS-------+

  (1) NetRexx Pipelines only

*/

options nostrictcase nostrictargs nostrictsignal nobinary

package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.

class change extends stage uses IRange

method run() public

   infinity = Rexx 999999999

   anyc = boolean 0
   args = Rexx ''
--   key = Rexx ''
--   rest = Rexx ''
   outl = Rexx ''
   ri = IRange[]

--   parse arg() key rest

--   key = key.upper()
   args = arg()

   anyc = getAnyCase(args, 3)
   if anyc then parse args . args

--   if 'ANYCASE'.abbrev(key, 3) | key.wordpos('CASEANY IGNORECASE CASEIGNORE') > 0 then do
--      anyc = 1
--      args = rest
--   end
--   else args = arg()

   ri = GetIRanges(args)
   nranges = ri[0].Number()
   args = ri[0].Residue()

   cmax = args.word(args.words())

   if cmax = '*' then do
     cmax = infinity
   end
   if cmax.datatype('W') then do
     args = args.delword(args.words())
     if args.word(args.words()) = 'FOR' then
       args = args.delword(args.words())
   end
   else cmax = infinity

   if cmax < 1 then do
      Emsg(11,'Error - 'getName()' number 'cmax' must be a positive numeric or "*"')
   end

   from_string = ''
   to_string = ''

   ftw = getKeyWord(args, 'FROMTO', 0, 'K')
   if ftw[2] then do
     parse args . args
     parse args delim 2 fp (delim) tp (delim)
     if args.right(1) = delim & args.countstr(delim) = 3 then do
       from_string = fp
       to_string = tp
     end
   end

   fwk = getKeyWord(args, 'FROM', 0, 'D')
   if fwk[2] <> '' then do
     from_string = fwk[2]
     args = fwk[1]
   end

   twk = getKeyWord(args, 'TO', 0, 'D')
   if twk[2] <> '' then do
     to_string = twk[2]
     args = twk[1]
   end

   if args <> '' then do
     args = args.strip()
     parse args delim 2 fp (delim) tp (delim)
     if args.right(1) = delim & args.countstr(delim) = 3 then do
       from_string = fp
       to_string = tp
       args = ''
     end
   end

   if args <> '' then do
     fds = DString(args)
     from_string = fds.String
     tds = DString(fds.Residue)
     to_string = tds.String
     if tds.Residue <> '' then
       Emsg(11, 'Change: extra unknown:' tds.Residue)
   end

   sec_out = boolean 0              -- set flag if output 1 is connected AT START
   do
      selectOutput(1)
      sec_out = 1
   catch StageError
   end

   do                          -- back to output 0
      selectOutput(0)
   catch StageError
   end

   if nranges = 0 then sr = 1
   else do
      sr = ri[1].StartCol()
      if ri[1].Type() \= 'C' then sr = 0
   end

   select
   when from_string == '' & to_string \== '' & (sr = 1 | nranges = 0 ) then -- only a "to_string" zone is col 1
   loop forever
      line = Rexx peekto()
      outl = to_string || line
      chang = 1

      if sec_out then do
         if chang then output(outl)
         else do
            selectOutput(1)
            output(outl)
            selectOutput(0)
         end
      catch StageError
      end
      else output(outl)

      readto()
   catch StageError
   end
   when from_string == '' & to_string \== '' & sr = 0 then do   -- only a "to_string" in indertminate zone
      loop forever
         line = Rexx peekto()

         ml = ri[1].SelIRange(line)
         s = ri[1].StartChar()
         select
           when s = 0 then do
              outl = ''
              chang = 0
           end
           when s = 1 & line \= '' then outl = to_string || line
           when s > line.length() then outl = line
           otherwise
              parse line l1 =(s) z
              outl = l1 || to_string || z
         end

         if sec_out then do
            if chang then output(outl)
            else do
               selectOutput(1)
               output(outl)
               selectOutput(0)
            end
         catch StageError
         end
         else output(outl)

         readto()
      catch StageError
      end
   end
   when from_string == '' & to_string \== '' then do   -- only a "to_string" in zone > 1
      sm = sr - 1
      loop forever
         chang = 1
         line = Rexx peekto()
         parse line l1 =(sr) z
         select
            when z \= '' then outl = l1 || to_string|| z
            when line.length() = sm then outl = line || to_string
            otherwise
               outl = l1
               chang = 0
         end

         if sec_out then do
            if chang then output(outl)
            else do
               selectOutput(1)
               output(outl)
               selectOutput(0)
            end
         catch StageError
         end
         else output(outl)

         readto()
      catch StageError
      end
   end
   when anyc then do               -- have an ANYCASE "from_string" and maybe a "to_string"
                               -- How should to_string be capitalized?
      ul = 0
      ll = 0

      loop i = 1 to from_string.length() until ul
         ch = from_string.substr(i,1)
         select
            when \ch.datatype('M') then nop
            when ch.datatype('U') then ul = 1
            otherwise ll = 1
         end
      end

      xcase = \ul | \ll
      if xcase then do
         parse to_string t1 2 tr
         tof = t1.upper() || tr.lower()
         tou = to_string.upper()
         tol = to_string.lower()
      end

      from_string = from_string.upper()
      add = from_string.length()
      add1 = add + 1

      loop forever                        -- anycase
         line = Rexx peekto()
         if nranges = 0 then nranges = 1
         pl = ''
         i = 1
         loop j = 1 to nranges
            ml = ri[j].SelIRange(line).upper()
            p = 1 - add
            loop i = i while i <= cmax        -- find FROMs, from the left
               p = ml.pos(from_string, p + add)
               if p = 0 then leave
               if pl.wordpos(p) = 0 then pl = pl p + ri[j].StartChar() - 1
               else i = i - 1
            end
         end
         outl = line
         chang = i > 1
         loop i = i - 1 to 1 by -1         -- change FROMs to TOs, from the right
            p = pl.word(i)
            l = ''
            if p = 1 then parse outl =(add1) r1
            else parse outl l =(p) . +(add) r1

            if xcase then do
               parse line =(p) c1 +1 c2 +1
               select
                  when (c1''c2).datatype('U') then to_string = tou
                  when c1.datatype('U') then to_string = tof
                  otherwise to_string = tol
               end
            end

            outl = l || to_string || r1
         end

         if sec_out then do
            if chang then output(outl)
            else do
               selectOutput(1)
               output(outl)
               selectOutput(0)
            end
         catch StageError
         end
         else output(outl)

         readto()
      catch StageError
      end

   end
   otherwise                               -- have an exact "from_string" and maybe a "to_string"
      add = from_string.length()
      add1 = add + 1
      loop forever
         line = Rexx peekto()
         if nranges = 0 then nranges = 1
         pl = ''
         i = 1
         loop j = 1 to nranges
            ml = ri[j].SelIRange(line)
            p = 1 - add
            loop i = i while i <= cmax        -- find FROMs, from the left
               p = ml.pos(from_string, p + add)
               if p = 0 then leave
--               pp = p + ri[j].StartChar() - 2
               pp = p + ri[j].StartChar() - 1
               if pl.wordpos(pp) = 0 then pl = pl pp
               else i = i - 1
            end
         end
         outl = line
         chang = i > 1
         loop i = i - 1 to 1 by -1         -- change FROMs to TOs, from the right
            p = pl.word(i)
            l = ''
            if p < 2 then parse outl =(add1) r1
--            else parse outl l +(p) . +(add) r1
            else parse outl l =(p) . +(add) r1
            outl = l || to_string || r1
         end

         if sec_out then do
            if chang then output(outl)
            else do
               selectOutput(1)
               output(outl)
               selectOutput(0)
            end
         catch StageError
         end
         else output(outl)

         readto()
      catch StageError
      end
   end
   rc = mrc()

exit(rc*(rc<>12))
