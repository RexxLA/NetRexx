-- decode64.nrx NJPipe Stage
/*
 *  Copyright (C) 2020 Jeffrey Hennick
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/*
 20/10/16 New. Jeff Hennick
*/

/** decode64

                      +-MIME--+
   >>--+-64DECODE-+---+-------+--><
       +-DECODE64-+   +-BASIC-+
                      +-URL---+
*/

/*
   NOTE: CMS is only 64DECODE, and does not have the options; it does MIME.
*/

options nostrictcase nostrictargs nostrictsignal
--package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.
import java.util.Base64

class decode64 extends stage
  method run()

  parse arg().upper opt restup

  if restup <> '' then do
    parse arg() opt rest
    Emsg(11, 'Extra argument found:' rest'.')
  end

  select case opt
    when '', 'MIME' then
      dec = Base64.getMIMEDecoder()
    when 'BASIC' then
      dec = Base64.getDecoder()
    when 'URL' then
      dec = Base64.getUrlDecoder()
    otherwise
      Emsg(11, 'Unrecognised argument found:' arg()'.')
  end

    loop label Main forever
      record_in = peekto().toString()
      ob = dec.decode(record_in)
      s = String(ob)
      output(Rexx s)
      readto()
    catch stageError
      rc = rc()
    end Main
    Exit(rc*(rc<>12))
