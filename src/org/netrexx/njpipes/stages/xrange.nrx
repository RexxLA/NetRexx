-- xrange.nrx NJPipe Stage
/*
 *  Copyright (C) 2020 Jeffrey Hennick Jeff (at) Jeff (dash) H (dot) com
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/** xrange

 >>--XRANGE--+---------------------------------+----><


  Note: njPipes uses ASCII and CMS uses EBCDIC

*/

/*
  20/ 6/10  New.  Jeff Hennick
*/

options binary nostrictcase nostrictargs nostrictsignal
package org.netrexx.njpipes.stages
import org.netrexx.njpipes.pipes.

class xrange extends stage uses java.lang.System

method run() public

   a = arg()
   ar = Rexx a
   parse ar fw sw tw

   if tw \= '' then
      Emsg(11,'Error - 'getName()' must have only one or two words of options:' ar)
   if fw = '' then
      Emsg(11,'Error - 'getName()' must have one or two words of options.')

   if sw \= '' then do  -- two words
     fc = getXorc(fw)
     lc = getXorc(sw)
   catch BadArgumentException
      Emsg(11,'Error - 'getName()' Bad hexadecimal:' ar)
   end
   else do              -- one word
     parse fw fp '.' len
     if len = '' then do
       parse fw fp '-' lp
       fc = fp.d2c()
       lc = lp.d2c()
     end
     else do
       fc = fp.d2c()
       lc = (fp + len -1).d2c()
     catch NumberFormatException
      Emsg(11,'Error - 'getName()' Bad decimal number:' ar)
     end
   end

   o = Rexx ''
   loop i = fc.c2d() for (lc.c2d() - fc.c2d() + 1)
     o = o''i.d2c()
   end

  do
      output(o)
   catch StageError
      rc = rc()
   end

   exit(rc * (rc<>12))
