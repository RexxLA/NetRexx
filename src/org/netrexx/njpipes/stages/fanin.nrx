-- fanin.nrx NJPipe Stage
/*
 *  Copyright (C) 1998  Jeffrey Hennick, JHennick@Delphi.Com
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/*
 20/ 4/21 Fix stream option. Was loop i = 0.  Jeff Hennick
 98/ 6/10 Up to CMS documentation.  Jeff Hennick
*/

/** fanin

   >>--FANIN--+------------+----------------------------------------------><
              | <--------+ |
              +---stream-+-+
*/
options nostrictcase nostrictargs nostrictsignal
package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.

class fanin extends stage

method run()

   list = rexx arg()

   rc = int 0

   if list = '' then loop i=0 to maxInputStream()
      if rc = 12 then rc = 0

      do
         selectInput(i)
      catch StageError
         rc = rc()
      end

      loop
         line = peekto()
         output(line)
         readto()
      catch StageError
         rc = rc()
      end

   end i
   else loop i=1 to list.words()
      str = rexx list.word(i)

      do
         selectInput(str)
      catch StageError
         rc = rc()
      end

      loop
         line = peekto()
         output(line)
         readto()
      catch StageError
            rc = rc()
      end

   end i

exit(rc*(rc<>12))
