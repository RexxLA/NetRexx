-- encode64.nrx Pipeline stage
/*
 *  Copyright (C) 2020 Jeffrey Hennick
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/*
  23/ 2/20 Documentation; givehelp(). Jeff Hennick
  20/10/16 New. Jeff Hennick
*/

options nostrictcase nostrictargs nostrictsignal
package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.
import java.util.Base64

class encode64 extends stage

method run()

  parse arg().upper opt restup

  if restup <> '' then do
    parse arg() opt rest
    Emsg(11, 'Extra argument found:' rest'.')
  end

  select case opt
    when '', 'MIME' then
      enc = Base64.getMIMEEncoder()
    when 'BASIC' then
      enc = Base64.getEncoder()
    when 'URL' then
      enc = Base64.getUrlEncoder()
    otherwise
      Emsg(11, 'Unrecognised argument found:' arg()'.')
  end

  loop label Main forever

    record_in = Rexx peekto()

    ba = record_in.toByteArray()

    opr = Rexx enc.encodeToString(ba)

    output(opr)

    readto()

  catch stageError
    rc = rc()
  end Main

  Exit(rc*(rc<>12))

Method givehelp() static
return -
"/** encode64\n" -
"                         +-MIME-(1)--+\n" -
"   >>--+-64ENCODE-----+--+-----------+--><\n" -
"       +-ENCODE64-(1)-+  +-BASIC-(1)-+\n" -
"                         +-URL-(1)---+\n" -
"\n" -
"   (1) NetRexx Pipelines only.\n" -
"   NOTE: CMS is only 64ENCODE, and does not have the options; it does MIME only.\n" -
"*/\n"
