-- strliteral.nrx Pipeline stage
/*  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/*
  23/02/23 Documentation; givehelp(). Jeff Hennick
  20/ 5/24 CONDitional, IFEMPTY added. Jeff Hennick
  98/ 6/12 DString.  New; from LITERAL.  JHennick
*/

options nostrictcase nostrictargs nostrictsignal
package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.

class strliteral extends stage

method run()
   args = arg()
   parse args.upper() w1 .

   cond = 0
   pref = (w1 = 'PREFACE')
   appe = (w1 = 'APPEND')
   ifempty = (w1 = 'IFEMPTY')
   if ifempty then
     parse args . args

   if pref | appe then do
     parse args . args
     if 'CONDITIONAL'.abbrev(args.word(1).upper, 4) then do
       cond = 1
       parse args . args
     end
   end
   else do
     pref = 1
     if 'CONDITIONAL'.abbrev(args.word(1).upper, 4) then do
       cond = 1
       parse args . args
     end
   end

 --  pref = 1
   if 'CONDITIONAL'.abbrev(args.word(1).upper, 4) then do
     cond = 1
     parse args . args
   end

   str = DString(args).String

   do
      if pref & \cond & \ifempty then output(str)
      written = 0
      select
         when outStreamNumber()=-99 then
            Emsg(11,'Error - no output stream connected')
         when inStreamNumber()=-99 then do
            if ifempty then output(str)
            exit(0)
         end
         when appe then loop                -- append after stream
            d = peekto()
            output(d)
            written = 1
            readto()
         catch StageError
            rc = rc()
            if rc = 12 then do
               if cond then cond = \written
               if \cond then output(str)
            catch StageError
               rc = rc()
            end
         end
         when cond then do
            d = peekto()
            output(str)
            shortStreams()
            catch StageError
               rc = rc()
         end
         otherwise
            shortStreams()                  -- get out of the picture
      end
      catch StageError
         rc = rc()
      end

   exit(rc*(rc<>12))

method givehelp() static
return -
"/** strliteral -- Write the Argument String\n" -
"\n" -
"   >>--STRLITeral--+------------------------------+--->\n" -
"                   | +-PREFACE-+                  |\n" -
"                   +-+---------+--+-------------+-+\n" -
"                   | +-APPEND--+  +-CONDitional-+ |\n" -
"                   +-IFEMPTY----------------------+\n" -
"\n" -
"   >-+-----------------+-----------><\n" -
"     +-delimitedString-+\n" -
"*/\n"
