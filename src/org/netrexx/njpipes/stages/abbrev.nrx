-- abbrev.nrx Pipeline stage
/*
 *  Copyright (C) 1998  Jeffrey Hennick, JHennick@Delphi.Com
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/*
   23/ 2/17 Documentation; givehelp(). Jeff Hennick
   20/ 4/ 5 Removed the long name classes to individual files. jh
   19/09/16 Add CASELESS to bring up to current CMS. JHennick
   99/05/09  use mrc() instead of rc().  ejt
   98/06/20 aobj; binary.  JHennick
*/


package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.

class abbrev extends stage binary

method run() signals ThreadQ

   parse arg() word number cf .

   cf = cf.upper()
   anyc = boolean 'ANYCASE'.abbrev(cf, 3) | 'CASEANY IGNORECASE CASEIGNORE CASELESS'.wordpos(cf) > 0

   if \number.datatype('W') then number = 0

   sec = boolean 0
   do
      selectOutput(1)
      sec = 1
   catch StageError
   finally
      do
         selectOutput(0)
      catch StageError
      end
   end

   if anyc then do                 -- ANYCASE
      word = word.upper()
      if sec then do                 -- secondary connected
         loop
            aobj = peekto()
            line = Rexx aobj
            parse line w1 .

            if word.abbrev(w1.upper(),number) then output(line)
            else do
               selectOutput(1)
               output(aobj)
               selectOutput(0)
            catch StageError
            end

            readto()
         catch StageError
         end
      end
      else do                        -- primary only
         loop
            aobj = peekto()
            line = Rexx aobj
            parse line w1 .

            if word.abbrev(w1.upper(),number) then output(aobj)

            readto()
         catch StageError
         end
      end
   end
   else do                 -- \ANYCASE
      if sec then do                 -- secondary connected
         loop
            aobj = peekto()
            line = Rexx aobj
            parse line w1 .

            if word.abbrev(w1,number) then output(aobj)
            else do
               selectOutput(1)
               output(aobj)
               selectOutput(0)
            catch StageError
            end

            readto()
         catch StageError
         end
      end
      else do                        -- primary only
         loop
            aobj= peekto()
            line = Rexx aobj
            parse line w1 .

            if word.abbrev(w1,number) then output(aobj)

            readto()
         catch StageError
         end
      end
   end

   rc = mrc()
   exit(rc*(rc<>12))


method givehelp() static
return -
"/** abbrev -- Select Records that Contain an Abbreviation of a Word in the First Positions\n" -
"\n" -
">>--ABBREViation-(1)--+-------------------------------+---><-\n" -
"                      +-word-+---------------------+--+\n" -
"                             +-number-+---------+--+\n" -
"                                      +-ANYcase-+ (2)(3)\n" -
"\n" -
"(1) ABBREViation must be ABBREV in CMS\n" -
"(2) CASEANY, CASEIGNORE, CASELESS, and IGNORECASE are all synonyms for ANYcase.\n" -
"(3) ANYcase must be ANYCASE in CMS\n" -
"\n" -
"*/\n"
