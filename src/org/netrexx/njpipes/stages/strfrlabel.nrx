-- strfrlabel.nrx NJPipe Stage
/*
 *  Copyright (C) 1998  Jeffrey Hennick, JHennick@Delphi.Com
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.  
 */
-- 98/ 6/12  DString.  New; from FRLABLE.  JHennick

/** strfrlable

       +-FRLABEL--+--------+----------------------+
       |          +-string-+                      |
   >>--+-STRFRLABEL--+---------+--delimitedString-+-----------------------><
                     +-ANYcase-+

  Purpose: read records searching for a string starting in column 1. copy all records
           thereafter (including the record containing the label) to stream 0.
           copy all preceding records to stream 1 (if it exists).

           a null argument causes stream 0 to be shorted to output (0).

  FRLABLE Contributed by Chuck Moore.

**/
options nostrictcase nostrictargs nostrictsignal
package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.

class strfrlabel extends stage

method run()

   /* variable declarations */
   _labelFound   = boolean  0
   _rc           = int      0
   _Label_len    = int      0
   _output1      = boolean  1
   _output0      = boolean  1

   args          = rexx     ''
   anyc          = boolean  0
   w1            = rexx     ''

   /* get incoming arguments and associated parameters */

   parse arg() args
   parse args.upper() w1 .
   anyc = 'ANYCASE'.abbrev(w1,3) | 'CASEANY IGNORECASE CASEIGNORE'.wordpos(w1)
   if anyc then parse args . args

   Label = DString(args).String
   if anyc then Label = Label.upper()

   _Label_len = Label.length()               -- handy to have around for speed


   /* find out which streams we have available */
   do                                           -- enable catch
      selectOutput(1)

   catch StageError
      _output1 = 0
   end

   do                                           -- enable catch
      selectOutput(0)

   catch StageError
      _output0 = 0
   end


   select
       when \ _output0 then
            _rc = 13                            -- no reason to continue without stream 0
       when _Label_len <> 0 & anyc then
            loop forever until _labelFound
               _record = Rexx peekto()          -- look at the next record
               if Label = _record.left(_Label_len).upper() then
                  do
                     _labelFound = 1
                     selectOutput(0)
                     shortStreams()

                  catch StageError           -- errors (or end of datastream)
                     _rc = rc()
                     if _rc = 4 then _rc = 12
                  end
               else
                    do
                       if _output1 then
                          do
                             selectOutput(1)
                             output(_record)
                          catch StageError
                             _rc = rc()
                          end
                    end

               if \ _labelFound then
                  readto()

            catch StageError                 -- errors (or end of datastream)
               _rc = rc()
            end
       when _Label_len <> 0 then
            loop forever until _labelFound
               _record = Rexx peekto()          -- look at the next record
               if Label = _record.left(_Label_len) then
                  do
                     _labelFound = 1
                     selectOutput(0)
                     shortStreams()

                  catch StageError           -- errors (or end of datastream)
                     _rc = rc()
                  end
               else
                    do
                       if _output1 then
                          do
                             selectOutput(1)
                             output(_record)
                          catch StageError
                             _rc = rc()
                          end
                    end

               if \ _labelFound then
                  readto()

            catch StageError                 -- errors (or end of datastream)
               _rc = rc()
            end
       otherwise
            do
               selectOutput(0)
               shortStreams()

            catch StageError                 -- errors (or end of datastream)
               _rc = rc()
            end
   end

   exit(_rc * (_rc <> 12) )        -- sever streams and leave with Colishaw (tm) retcode

