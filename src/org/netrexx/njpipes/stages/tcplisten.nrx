-- tcplisten.nrx Pipeline stage
/*
 *  Copyright (C) 1997-2011 Ed Tomlinson
 *
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 *
 */

/*
  23/02/24 Documentation; givehelp(). Jeff Hennick
  98/10/01  add input stream support. ejt
  98/08/28  fixed a commit problem.  ejt
*/

package org.netrexx.njpipes.stages

options binary nostrictcase nostrictargs nostrictsignal

import org.netrexx.njpipes.pipes.

class tcplisten public extends stage

s0 = boolean
e  = IOException null

method run()

   a = arg()

   p = a.word(1)

   if p.datatype('W') then
      port = int p
   else do
      Emsg(11,'Error - expected a port number, got 'p)
   end

   timeout = int 60000
   i = a.wordpos('timeout')
   if i>0 then do
      timeout = int a.word(i+1)
      a = a.delword(i,2)
   end

   backlog = int 10
   i = a.wordpos('backlog')
   if i>0 then do
      backlog = int a.word(i+1)
      a = a.delword(i,2)
   end

   do
      s0 = 1
      selectInput(0)
   catch StageError
      s0 = 0
   end

   do
      commit(-1)

      ear = ServerSocket(port,backlog)
      ear.setSoTimeout(timeout)

      loop forever
         do
            if s0 then
               peekto()
            z = counter(1)
            soc = ear.accept()
            z = counter(z)
--          dump()
            output(soc)
            if s0 then
               readto()
         catch e=IOException
--          say e
            z = counter(z)
            if InterruptedIOException>=e then
               selectOutput(0)
            else
               signal e
         end
      end

   catch StageError
      rc = rc()

   catch e=IOException

   finally
      if ear<>null then do
         ear.close()
      catch IOException
      end

      if e<>null then
         Emsg(12,'Error - tcplisten gets: 'e.getMessage)

   end

do
   selectOutput(1)
   output(Rexx rc)
catch StageError
end

exit(rc*(rc<>12))

method givehelp() static
return -
"/** tcplisten -- Listen on a TCP Port\n" -
"\n" -
"                       +---------------------------------------+\n" -
">>--TCPLISTEN--number--v-+----------------------------------+--+--><\n" -
"                         +-BACKLOG--number------------------+\n" -
"                         +-GETSOCKName-(2)------------------+\n" -
"                         |                +-ANY-------+     |\n" -
"                         +-LOCALIPaddress-+-HOSTID----+-(2)-+\n" -
"                         |                +-IPaddress-+     |\n" -
"                         +-REUSEADDR-(2)--------------------+\n" -
"                         +-STATistics-(2)-------------------+\n" -
"                         +-USERid--word-(2)-----------------+\n" -
"                         +-TIMEOUT--number-(1)--------------+\n" -
"\n" -
"(1) NetRexx Pipelines only.\n" -
"(2) CMS Pipelines only.\n" -
"\n" -
"\n" -
"Simple tcplisten implementation.  You can only supply the port and a timeout\n" -
"value.  Its ignored unless tcplisten's output stream has been severed, in\n" -
"which case tcplisten terminates.\n" -
"\n" -
"If input stream 0 is connected, tcplisten does a peekto before calling the\n" -
"accept method.  The object is consumed after the output of the socket object\n" -
"returns.\n" -
"\n" -
"*/\n"
