-- hostname.nrx Pipeline stage
/*
 *  Copyright (C) 2020 RexxLA
 *
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 *
 */

   /*  Change History:
   *
   *      2023/02/21 Documentation; givehelp(). Jeff Hennick
   *      2020-06-01   CK        - Initial kick
   */

options binary

import org.netrexx.njpipes.pipes.
import java.net.

package org.netrexx.njpipes.stages

class hostname extends stage

  method run() signals ThreadQ

    a                = Rexx             /* arguments passed to filter                                           */
    localhost        = Rexx             /* Rexxified output of Java getLocalHost                                */
    hostname         = Rexx             /* hostname of system                                                   */
    ipaddress        = Rexx             /* IP address of system                                                 */
    includeip        = 0                /* flag for optional inclusion of hosts ip address on output            */

    if \isFirstStage() then do
       say "PIPE Error: "this.getName()" stage must be the first stage of a pipeline."
       rc = 83
       exit( rc )
    end

    /* -- Process any arguments -- */

        a = Rexx (arg(null))

        if a \= null then do

           a = a.upper()
           if a.pos("INCLUDEIP") > 0 then includeip = 1

        end

    /* -- Main loop -- */

    do
       loop label siempre    forever

        localhost = Rexx InetAddress.getLocalHost();  /* returns hostname/ipaddress */
       parse localhost hostname "/" ipaddress
       if includeip then output(hostname"/"ipaddress)
                      else output(hostname)
       exit( rc() )

        catch java.net.UnknownHostException
           do

             output("# Unable to determine hostname for this system #")
             exit( rc() )

           end   /* catch Unknown Host */

       end /* loop forever */

       catch StageError
         rc = rc()

     end /* do (loop forever) */

    exit(rc*(rc \= 12))

Method givehelp() static
return -
"  /**  hostname - Write default IP host name of the system\n" -
"   *\n" -
"   *  hostname writes a single output record which contains the TCP/IP name of the system.\n" -
"   *\n" -
"   *  hostname must be in the first stage of a pipe.  It does not consume any records.\n" -
"   *\n" -
"   *  >>--HOSTNAME ---+-----------------+-------------->< (2)\n" -
"   *                  +--- INCLUDEIP ---+ (1)\n" -
"   *\n" -
"   *  (1) Optional parameter not present in VM/CMS version\n" -
"   *  (2) The USERid option available under CMS is not applicable and not implemented\n" -
"   *\n" -
"   *  Arguments:  INCLUDEIP - include the IP address of the system in the response\n" -
"   *                          in the form <hostname>/<ip address>\n" -
"   *\n" -
"   *  Examples:\n" -
"   *\n" -
"   *  Z:\\>pipe \"hostname | console\"\n" -
"   *  DESKTOP-C91GVBB\n" -
"   *\n" -
"   *  Z:\\>pipe \"hostname includeip | console\"\n" -
"   *  DESKTOP-C91GVBB/10.0.2.15\n" -
"   *\n" -
"   *  Z:\\>pipe \"literal def | hostname includeip | console\"\n" -
"   *  PIPE Error: hostname_2 stage must be the first stage of a pipeline.\n" -
"   *  RC=83\n" -
"   */\n"
