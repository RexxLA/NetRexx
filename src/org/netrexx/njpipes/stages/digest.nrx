-- digest.nrx Pipeline stage
/*
 *  Copyright (C) 2020 rvj
 *  based on Jeff's encode64
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/*
  23/ 2/20 Documentation; givehelp(). Jeff Hennick
  21-08-17 New. rvj
*/


options nostrictcase nostrictargs nostrictsignal
package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.
import java.security.MessageDigest

class digest extends stage

method run()
  parse arg().upper opt restup

  if restup <> '' then do
    parse arg() opt rest
    Emsg(11, 'Extra argument found:' rest'.')
  end

  do
    select case opt
      when '', 'SHA1' then
        md = MessageDigest.getInstance('SHA1')
      otherwise
        md = MessageDigest.getInstance(opt)
    end
  catch java.security.NoSuchAlgorithmException
    say 'No such digest algorithm implemented'
    Emsg(11, 'Unrecognised argument found:' arg()'.')
    rc = 16
  end -- do

  do
    loop forever
      record_in = Rexx peekto()      -- get input
      ba = record_in.toByteArray()   -- convert to ByteArray
      md.update(ba)                  -- update the digest
      readto()
    catch org.netrexx.njpipes.pipes.StageError
      rc = rc()
    end -- do

    end
  do
--    s=Rexx
    opr = md.digest()           -- when done, compute digest
    hexString = StringBuffer()
    loop i=0 to opr.length-1
      hex = Integer.toHexString(0xFF & opr[i])
      if (hex.length() == 1) then do
        hexString.append('0');
      end
      hexString.append(hex);
    end
    output(Rexx hexString)
    -- output((String opr).getBytes())
  catch org.netrexx.njpipes.pipes.StageError
    rc = rc()
  end -- do

  Exit(rc*(rc<>12))


Method givehelp() static
return -
"/** digest -- Compute a Message Digest\n" -
"\n" -
"   >>--DIGEST--+-SHA1-----------+--+------------------------+--><\n" -
"               +-SHA224---------+  +--APPEND-(1)------------+\n" -
"               +-SHA256---------+  +--VERIFY-(1)-+----------+\n" -
"               +-SHA384---------+                +--NOT-(1)-+\n" -
"               +-SHA512---------+\n" -
"               +-MD5------------+\n" -
"               +-MD2-(2)--------+\n" -
"               +-SHA512/224-(2)-+\n" -
"               +-SHA512/256-(2)-+\n" -
"\n" -
"(1) CMS Pipelines only.\n" -
"(2) NetRexx Pipelines only (dependent on the JVM implementation).\n" -
"\n" -
"   NetRexx Pipelines returns the bytearray as a HEX string\n" -
"   CMS returns a char array into the pipeline\n" -
"*/\n"
