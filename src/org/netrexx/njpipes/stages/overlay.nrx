-- overlay.nrx njPipes stage
/*
 *  Copyright (C) 1997-2020 Ed Tomlinson
 *
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 *
 */
/*
  20/ 5/10 Double fail for selectInput (it was quitting too soon-?-). Note. Jeff Hennick
  20/ 5/ 5 Fixed with .toString; overlayx(). Jeff Hennick
  97/  /   Ed Tomlinson
*/

/** OVERLAY

                 +-BLANK-+
 >>--OVERlay-(1)-+-------+----------------------------------------------><
                 +-xorc--+

   (1) OVER is only CMS, njPipes has a totally different OVER stage

*/
options nostrictcase nostrictargs nostrictsignal
package org.netrexx.njpipes.stages
import org.netrexx.njpipes.pipes.
options binary

class overlay extends stage

method run()
  arg=arg()
  pad=Rexx ' '
  if arg<>'' then
    do
      pad=GetXorc(arg)
      if pad=null then
        Emsg(11,'Error - 'getName()' option 'arg' is not recognized')
    end
  startpos=Rexx 1
  kicking=boolean 1
  loop label read while kicking   -- more fun than a slimmer "until"!
    kicking=boolean 0
    folded=Rexx ''
    loop pick=0 to maxInputStream()
      do
        selectInput(pick)
      catch StageError
        do
          selectInput(pick)
        catch StageError
          rc = rc()
          if pick = 0 then leave
          iterate
        end
      end
      selectInput(pick)
      line=Rexx peekto().toString()
      folded = overlayx(folded,line,startpos,line.length,pad)
      kicking=1
      readto()
    catch StageError
      rc=rc()
    end
    if kicking then
      output(folded)
  catch StageError
    rc=mrc()
  end read
  exit(rc*(rc<>12))

method overlayx(string,line,startpos,llength,pad)
     -- make them the same length
  slength = string.length
  if slength > llength then do
    line = line.left(slength,' ')
    max = slength
  end
  else do
    string = string.left(llength,' ')
    max = llength
  end
      -- work them
  o = ''
  if startpos > 1 then o = string.left(startpos - 1,' ')
  loop i = startpos to max
    cl = line.substr(i,1)
    if cl = pad then
      o = o''string.substr(i,1)
    else
      o = o''cl
  end
  return o
