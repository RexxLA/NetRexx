-- fblock.nrx NJPipe Stage
/*
 *  Copyright (C) 1998  Jeffrey Hennick, JHennick@Delphi.Com
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.  
 */
-- 99/05/09  use mrc() instead of rc().  ejt
-- 98/06/24 New. JHennick

/** fblock

   >>--FBLOCK--number--+------+-------------------------------------------><
                       +-xorc-+

*/

package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.

class fblock extends stage

method run()

   args = rexx arg()

   parse args number xorc

   pad = GetXorc(xorc)

   eof = 0
   data = Rexx ''
   loop until eof
      loop until data.length > number
         robj = Rexx readto()
         data = data || robj
      catch StageError
         eof = 1
      end

      loop while data.length >= number
         parse data out +(number) data
         output(out)
      catch StageError
      end

      if eof & data \== '' then do
         if pad \= null then data = data.left(number, pad)
         output(data)
      catch StageError
      end
   end

rc = mrc()
exit(rc*(rc<>12))

