/*
 *  Copyright (C) 1997-2011 Ed Tomlinson
 *
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 *
 */

package org.netrexx.njpipes.stages

-- 99/05/29 use byte[] directly.  ejt
-- 98/09/06 added stream option

import org.netrexx.njpipes.pipes.

class diskw extends stage

method run() public

rc  = int 0
blk = boolean 1

bo = [byte 0]
bc = Class

    a = arg()
    if a.word(1).upper()='STREAM' then do
       blk = 0
       a = a.subword(2)
    end


    do
       peekto()                                  -- do nothing if no input
       do
          out0 = boolean 1                       -- pass thru if connected
          selectOutput(0)
       catch StageError
          out0 = 0
       end
       if blk then
          do
             outFile = FileWriter(String a)
             out = BufferedWriter(outFile)
             loop forever
                line = peekto()
                if line<>null then do
                   if String>=line then
                      d = String line
                   else
                      d = line.toString()
                   out.write(d,0,d.length())
                end
                out.newLine()
                if out0 then
                   output(line)
                readto()
             catch StageError
                signal StageError
             catch ee=IOException
                Emsg(13,'Error - 'ee.getMessage())
             finally
                do
                   out.close()
                catch ee=IOException
                   Emsg(13,'Error - 'ee.getMessage())
                end
             end
          end
       else
          do
             bc = bo.getClass()
             b = byte[]
             boutFile = DataOutputStream(FileOutputStream(String a))
             bout = BufferedOutputStream(boutFile)
             loop forever
                line = peekto()
                if line<>null then do
                   select
                      when bc.isInstance(line) then
                         b = byte[] line
                      when String>=line then
                         b = (String line).getBytes()
                      otherwise
                         b = line.toString().getBytes()
                   end
                   bout.write(b,0,b.length)
                end
                if out0 then
                   output(line)
                readto()
             catch StageError
                signal StageError
             catch ee=IOException
                Emsg(13,'Error - 'ee.getMessage())
             finally
                do
                   bout.close()
                catch ee=IOException
                   Emsg(13,'Error - 'ee.getMessage())
                end
             end
          end
    catch ee=IOException
       Emsg(13,'Error - 'ee.getMessage())
    catch StageError
       rc = rc()
    end

exit(rc*(rc<>12))
