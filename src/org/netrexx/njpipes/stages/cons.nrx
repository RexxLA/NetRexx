-- console.nrx NJPipe Stage
/*
 *  Copyright (C) 1998, 2020  Jeffrey Hennick, Jeff (at) Jeff (dash) H (dot) com
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/*
 20/11/ 3 add PRefix & PRompt; test for CMS keywords; use getKeyWord.  Jeff Hennick
 20/ 5/24 use \isFirstStage(). Jeff Hennick
 98/ 8/28 fixed a commit problem.  ejt
 98/ 6/12 DString.  Up to CMS Documentation.  JHennick
*/

/** console



   >>--+-CONSole--+--+----------------------+--+-----------------------------+-| CMSonly |---><
       +-TERMinal-+  +-EOF--delimitedString-+  +-PRfix--+-delimitedString-(1)+
                     +-NOEOF----------------+  +-PRompt-+


 CMS Only:
  -|-+----------------+-(2)-|-
     +-DIRECT---------+
     +-ASYNchronously-+
     +-DARK-----------+

Notes:
 (1) NetRexx only
     On first stage, delimitedString is put out as a prompt
     On other stages, each line is prefixed with deleimitedString
     Outout to next stage does NOT include delimitedString
     Either keyword can be used for either stage
 (2) CMS only

*/

options nostrictcase nostrictargs nostrictsignal
package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.

class cons extends stage

method run() public

   args = rexx ''
   eoff = rexx ''
   eof = rexx 1
   pr = rexx ''

   args = arg()

   eofs = getKeyWord(args, 'EOF', 3, 'D')
   if eofs[2] \= '' then
     eoff = eofs[2]
   args = eofs[1]

   neofs = getKeyWord(args, 'NOEOF', 5, 'K')
   eof = \neofs[2]
   args = neofs[1]

   pra = getKeyWord(args, 'PREFIX', 2, 'D')
   if pra[2] \= '' then
     pr = pra[2]
   args = pra[1]

   pra = getKeyWord(args, 'PROMPT', 2, 'D')
   if pra[2] \= '' then
     pr = pra[2]
   args = pra[1]

   cmsa = getKeyWord(args, 'DIRECT', 0, 'K')
   if cmsa[2] then
     Emsg(11, '"DIRECT" keyword is only in CMS.')
   args = cmsa[1]

   cmsa = getKeyWord(args, 'ASYNchronously', 4, 'K')
   if cmsa[2] then
     Emsg(11, '"ASYNchronously" keyword is only in CMS.')
   args = cmsa[1]

   cmsa = getKeyWord(args, 'DARK', 0, 'K')
   if cmsa[2] then
     Emsg(11, '"DARK" keyword is only in CMS.')
   args = cmsa[1]

   if args \= '' then
     Emsg(11, 'Extra arguments:' args)


   if isFirstStage() then do       -- First Stage:  INPUT
      commit(-1)
      if pr \= '' then
        RexxIO.pipeSay(pr)
      select
         when \eof then                     -- No EOF signalling
            loop forever
               counter(1)
               a = ask
               counter(-1)
               output(a)
            catch StageError
               rc = rc()
            end
         when eoff \= '' then               -- EOF /string/
            loop forever
               counter(1)
               a = ask
               counter(-1)
               if a==eoff then
                  leave
               output(a)
            catch NullPointerException
            catch StageError
               rc = rc()
            end
         otherwise                          -- EOF on null
            loop forever
               counter(1)
               a = ask
               counter(-1)
               if a=='' then
                  leave
               output(a)
            catch NullPointerException
            catch StageError
               rc = rc()
            end
         end
   end
   else                               -- Not first stage: Output
      loop forever
        line = peekto()
	-- as this is the only place 'say' is used legitimately
        RexxIO.pipeSay(pr''line)
	-- we use this multithread-safe implementation of RexxIO.say
         output(line)
         readto()
      catch StageError
         rc = rc()
      end

   exit(rc*(rc<>12))


