-- tag.nrx Pipeline stage
/*
 *  Copyright (C) 2020, Jeffrey Hennick
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program.
 */

/*
 23/02/23 Documentation; givehelp(). Jeff Hennick
 20/11/17 New. Jeff Hennick
*/


options nostrictcase nostrictargs nostrictsignal
package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.

class tag extends stage

method run()

  parse arg() tag .

  if tag = '' then
    Emsg(11, 'No tag name given.')

  do
    output('<' || arg() || '>')
  catch stageError
  end

  loop label Main forever

    record_in = peekto()

    output(record_in)

    readto()

  catch stageError
    do
      output('</' || tag || '>')
    catch stageError
    end
    rc = rc()
  end Main

  Exit(rc*(rc<>12))

method givehelp() static
return -
"/** tag -- Surrounds Input Records with a HTML tag and its End Tag\n" -
"\n" -
">>--TAG--word--+----------+----><\n" -
"               +--string--+\n" -
"\n" -
"Outputs a record: <word string>, then passes through all records\n" -
"on its primary input, and finally a record: </word>.\n" -
"\n" -
"*/\n"
