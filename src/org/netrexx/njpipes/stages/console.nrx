-- console.nrx NJPipe Stage
/*
 *  Copyright (C) 1998  Jeffrey Hennick, JHennick@Delphi.Com
 *  Distributed under the ICU 1.8.1 Licence with NO WARRANTIES of ANY kind.
 *  See LICENSE for the licence and information on using, copying, modifying,
 *  and distributing this program. 
 */
-- 98/ 8/28 fixed a commit problem.  ejt
-- 98/ 6/12 DString.  Up to CMS Documentation.  JHennick

/** console

                                               { CMS Only                 }
                                               {                          }
   >>--+-CONSole--+--+----------------------+- { -+----------------+----- } ----><
       +-TERMinal-+  +-EOF--delimitedString-+  {  +-DIRECT---------+      }
                     +-NOEOF----------------+  {  +-ASYNchronously-+      }
                                               {  +-DARK-----------+      }
*/

package org.netrexx.njpipes.stages

import org.netrexx.njpipes.pipes.

class console extends stage

method run() public

   w1 = rexx ''
   args = rexx ''
   eoff = rexx ''
   eof = rexx 1

   parse arg() w1 args

   w1 = w1.upper()

   select
      when w1 = 'EOF' then eoff = DString(args).String
      when w1 = 'NOEOF' then eof = 0
      otherwise nop
   end

   if inStreamState()>1 then do       -- First Stage:  INPUT
      commit(-1)
      select
         when \eof then                     -- No EOF signalling
            loop forever
               counter(1)
               a = ask
               counter(-1)
               output(a)
            catch StageError
               rc = rc()
            end
         when eoff \= '' then               -- EOF /string/
            loop forever
               counter(1)
               a = ask
               counter(-1)
               if a==eoff then
                  leave
               output(a)
            catch NullPointerException
            catch StageError
               rc = rc()
            end
         otherwise                          -- EOF on null
            loop forever
               counter(1)
               a = ask
               counter(-1)
               if a=='' then
                  leave
               output(a)
            catch NullPointerException
            catch StageError
               rc = rc()
            end
         end
   end
   else                               -- Not first stage: Output
      loop forever
         line = peekto()
         say line
         output(line)
         readto()
      catch StageError
         rc = rc()
      end

   exit(rc*(rc<>12))
