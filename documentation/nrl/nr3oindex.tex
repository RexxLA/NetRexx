\subsection{Indexed strings}\label{refoindex}
\index{Overview,indexed strings}
\index{Indexed strings,overview}
:p.
NetRexx provides indexed strings, adapted from the compound variables of
Rexx.  Indexed strings form a powerful "associative lookup",
or \emph{dictionary}, mechanism which can be used with a
convenient and simple syntax.
:p.
NetRexx string variables can be referred to simply by name, or also by
their name qualified by another string (the \emph{index}).  When an
index is used, a value associated with that index is either set:
\begin{verbatim}
fred=0         -- initial value
fred[3]='abc'  -- indexed value
\end{verbatim}
:pc.
or retrieved:
\begin{verbatim}
say fred[3]    -- would say "abc"
\end{verbatim}
:pc.in the latter case, the simple (initial) value of the variable is
returned if the index has not been used to set a value.  For example,
the program:
\index{Example,program}
\begin{verbatim}
bark='woof'
bark['pup']='yap'
bark['bulldog']='grrrrr'
say bark['pup'] bark['terrier'] bark['bulldog']
\end{verbatim}
:pc.would display
\begin{verbatim}
yap woof grrrrr
\end{verbatim}
:p.
Note that it is not necessary to use a number as the index; any
expression may be used inside the brackets; the resulting string is
used as the index.  Multiple dimensions may be used, if required:
\index{Example,program}
\begin{verbatim}
bark='woof'
bark['spaniel', 'brown']='ruff'
bark['bulldog']='grrrrr'
animal='dog'
say bark['spaniel', 'brown'] bark['terrier'] bark['bull'animal]
\end{verbatim}
:p.
which would display
\begin{verbatim}
ruff woof grrrrr
\end{verbatim}
:p.
\index{Programs,examples}
\index{Example,program}
\index{Example,indexed strings}
\index{Indexed strings,example}
Here's a more complex example using indexed strings, a test program with
a function (called a \emph{static method} in NetRexx) that removes
all duplicate words from a string of words:
\begin{verbatim}
/* justonetest.nrx -- test the justone function.      */
say justone('to be or not to be')  /* simple testcase */
exit

/* This removes duplicate words from a string, and    */
/* shows the use of a variable (HADWORD) which is     */
/* indexed by arbitrary data (words).                 */
method justone(wordlist) static
  hadword=0         /* show all possible words as new */
  outlist=''            /* initialize the output list */
  loop while wordlist&bslash.=''  /* loop while we have data */
    /* split WORDLIST into first word and residue     */
    parse wordlist word wordlist
    if hadword[word] then iterate /* loop if had word */
    hadword[word]=1 /* remember we have had this word */
    outlist=outlist word   /* add word to output list */
    end
  return outlist         /* finally return the result */
\end{verbatim}
:p.
Running this program would display just the four words
"\textbf{to}", "\textbf{be}", "\textbf{or}", and
"\textbf{not}".
